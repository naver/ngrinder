description = "ngrinder-core"

dependencies {
    api (project(":ngrinder-groovy"))
    api (group: "org.apache.commons", name: "commons-compress", version: "1.4.1")
    api (group: "com.github.oshi", name: "oshi-core", version: "5.2.2")

    implementation files("${rootDir}/lib/grinder-3.9.1-patch.jar")
    implementation (group: "org.pf4j", name: "pf4j", version: "3.0.1")
    implementation (group: "javax.servlet", name: "javax.servlet-api", version: "3.1.0")
    implementation (group: "commons-collections", name: "commons-collections", version: "3.2.1")
    implementation (group: "com.fasterxml.jackson.core", name: "jackson-annotations", version: jackson_version)
    implementation (group: "com.fasterxml.jackson.core", name: "jackson-databind", version: jackson_version)
    implementation (group: "net.java.dev.jna", name: "jna", version: "5.6.0")
    implementation (group: "commons-codec", name: "commons-codec", version: "1.14")
    implementation (group: "com.beust", name: "jcommander", version: "1.32")
    implementation (group: "ch.qos.logback", name: "logback-classic", version: "1.0.0")
    implementation (group: "org.slf4j", name: "slf4j-api", version: slf4j_version)
    implementation (group: "org.slf4j", name: "jcl-over-slf4j", version: slf4j_version)
    implementation (group: "org.hibernate", name: "hibernate-core", version: hibernate_version)
    implementation (group: "org.jboss.spec.javax.transaction", name: "jboss-transaction-api_1.2_spec", version: "1.0.1.Final")

    compileOnly (group: "org.projectlombok", name: "lombok", version: "1.18.8")
    annotationProcessor (group: "org.projectlombok", name: "lombok", version: "1.18.8")

    testImplementation (group: "org.easytesting", name: "fest-assert", version: "1.4")
    testImplementation (group: "org.mockito", name: "mockito-core", version: mockito_version)
}

task makeAgentDependencies {
    doFirst {
        def classpath = ""
        sourceSets.main.compileClasspath.each {
            def libName = it.getName()
            if (libName == "grinder-3.9.1.jar") {
                return
            }
            if (libName == "grinder-3.9.1-patch.jar") {
                classpath += "grinder-3.9.1;"
                return
            }
            def removedVersion = libName.findAll(/(.*?)-\d+/).join("")
            if (removedVersion == "") {
                return
            }
            classpath += "${removedVersion.substring(0, removedVersion.lastIndexOf("-"))};"
        }
        new File("${buildDir}/classes/java/main/dependencies.txt").write(classpath)
    }
}

processResources {
    filesMatching("internal.properties") {
       expand(["ngrinder_version" :project.version])
    }
}

tasks.jar.dependsOn makeAgentDependencies
