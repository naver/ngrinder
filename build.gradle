plugins {
  id "com.jfrog.bintray" version "1.8.5"
}

allprojects  {
  apply plugin: "idea"

  group = "org.ngrinder"
  version = "3.5.4"

  idea {
    module {
      outputDir file("build/classes/main")
      testOutputDir file("build/classes/test")
    }
  }

  if (project.convention.findPlugin(JavaPluginConvention)) {
    // Change the output directory for the main and test source sets back to the old path
    sourceSets.main.java.outputDir = new File(buildDir, "classes/main")
    sourceSets.test.java.outputDir = new File(buildDir, "classes/test")
  }
}

subprojects {
  apply plugin: "java"
  apply plugin: "maven-publish"
  apply plugin: "com.jfrog.bintray"

  compileJava.options.encoding = "UTF-8"
  compileTestJava.options.encoding = "UTF-8"

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  ext {
    profile = project.hasProperty("profile") ? profile : "production"
    esCheckModuleVersion = project.hasProperty("esCheckModuleVersion") ? esCheckModuleVersion : ""
    slf4j_version = "1.7.28"
    spring_security_version = "5.3.4.RELEASE"
    spring_boot_version = "2.3.3.RELEASE"
    svnkit_version = "1.8.3-scm1"
    hibernate_version = "5.4.20.Final"
    hazelcast_version = "4.0.2"
    mockito_version = "2.23.4"
    handlebars_version = "4.0.5"
    jackson_version = "2.11.2"
    groovy_version = project.property("groovy.version")
    junit_version = project.property("junit.version")
  }

  repositories {
    mavenCentral()
    maven { url "http://repo.springsource.org/release" }
    maven { url "http://maven.scm-manager.org/nexus/content/repositories/releases" }
  }

  test {
    testLogging.showStandardStreams = true
  }

  task javadocJar(type: Jar) {
    from javadoc.destinationDir
    classifier "javadoc"
  }

  task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
  }

  publishing {
    publications {
      nGrinerModules(MavenPublication) {
        from components.java
        artifact sourceJar
        artifact javadocJar
        pom {
          name = "org.ngrinder:${project.name}"
          description = "${project.name} module"
          url = "https://github.com/naver/ngrinder"
          licenses {
            license {
              name = "The Apache License, Version 2.0"
              url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
          }
//          Below field is required to publish to the Maven central. Please modify before publishing.
//          developers {
//            developer {
//              id = "{please_input_your_id}"
//              name = "{please_input_your_name}"
//              email = "{please_input_your_email}"
//            }
//          }
          scm {
            connection = "scm:git:git://github.com/naver/ngrinder.git"
            developerConnection = "scm:git:ssh://github.com/naver/ngrinder.git"
            url = "https://github.com/naver/ngrinder"
          }
        }
      }
    }
  }

  bintray {
    user =  project.hasProperty("bintrayUser") ? bintrayUser : ""
    key =  project.hasProperty("bintrayKey") ? bintrayKey : ""
    publications = ["nGrinerModules"]
    publish = true
    override = true

    pkg {
      repo = "ngrinder"
      name = project.name
      userOrg = "navercorp"
      licenses = ["Apache-2.0"]
      websiteUrl = "https://github.com/naver/ngrinder"
      issueTrackerUrl = "https://github.com/naver/ngrinder/issues"
      vcsUrl = "https://github.com/naver/ngrinder.git"
      labels = ["ngrinder"]
      publicDownloadNumbers = true
      version {
        name = project.version
        released = new Date()
        gpg {
          sign = true
          passphrase = project.hasProperty("bintrayGpgPassphrase") ? bintrayGpgPassphrase : ""
        }
      }
    }
  }

}
